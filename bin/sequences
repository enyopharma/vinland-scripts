#!/usr/bin/env perl
use strict;
use warnings;
use DBI;
use JSON qw(from_json);
use Sort::Key::Natural qw(natsort);
use Data::Dumper;

my $drakkar = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', 'drakkar', $ENV{'DB_PROD_HOSTNAME'}),
    $ENV{'DB_PROD_USERNAME'},
    $ENV{'DB_PROD_PASSWORD'},
);

my $vinland = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', 'vinland', $ENV{'DB_DEV_HOSTNAME'}),
    $ENV{'DB_DEV_USERNAME'},
    $ENV{'DB_DEV_PASSWORD'},
);

my $select_human_proteins_sth = $drakkar->prepare('
    SELECT p.id, p.accession, p.name, p.sequences
    FROM proteins AS p, proteins_versions AS pv
    WHERE p.type = \'h\'
    AND p.accession = pv.accession
    AND p.version = pv.version
    ORDER BY p.accession ASC
');

my $select_viral_proteins_sth = $drakkar->prepare('
    SELECT DISTINCT protein2_id AS id, accession2 AS accession, name2 AS name, start2 AS start, stop2 AS stop
    FROM dataset
    WHERE type = \'vh\'
    AND state = \'curated\'
    AND is_obsolete1 IS FALSE
    AND is_obsolete2 IS FALSE
    AND deleted_at IS NULL
    ORDER BY accession2 ASC
');

my $select_sequences_sth = $drakkar->prepare('
    SELECT sequences FROM proteins WHERE id = ?
');

my $select_protein_sth = $vinland->prepare('
    SELECT id FROM proteins WHERE accession = ? AND name = ?
');

my @sequences = ();

$select_human_proteins_sth->execute;

while (my $protein = $select_human_proteins_sth->fetchrow_hashref) {
    $select_protein_sth->execute($protein->{'accession'}, $protein->{'name'});
    my ($protein_id) = $select_protein_sth->fetchrow_array;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $protein->{'accession'}, $protein->{'name'})) unless $protein_id;

    my $sequences = from_json($protein->{'sequences'});

    foreach my $accession (natsort keys %{$sequences}) {
        push(@sequences, [
            $protein_id,
            $accession,
            $protein->{'accession'} eq $accession ? 1 : 0,
            0,
            $sequences->{$accession},
            1,
            length($sequences->{$accession}),
        ]);
    }
}

$select_human_proteins_sth->finish;

$select_viral_proteins_sth->execute;

while (my $protein = $select_viral_proteins_sth->fetchrow_hashref) {
    $select_protein_sth->execute($protein->{'accession'}, $protein->{'name'});
    my ($protein_id) = $select_protein_sth->fetchrow_array;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $protein->{'accession'}, $protein->{'name'})) unless $protein_id;

    $select_sequences_sth->execute($protein->{'id'});
    my ($sequences_str) = $select_sequences_sth->fetchrow_array;
    $select_sequences_sth->finish;

    my $pacc = $protein->{'accession'};
    my $sequences = from_json($sequences_str);

    my $canonical = $sequences->{$pacc};

    my $sequence = substr($canonical, $protein->{'start'} - 1, $protein->{'stop'} - $protein->{'start'} + 1);

    my $is_mature = length($canonical) == length($sequence) ? 0 : 1;

    push(@sequences, [
        $protein_id,
        $pacc,
        1,
        $is_mature,
        $sequence,
        $protein->{'start'},
        $protein->{'stop'},
    ]);

    next if $is_mature;

    foreach my $iacc (natsort keys %{$sequences}) {
        next if ($pacc eq $iacc);

        push(@sequences, [
            $protein_id,
            $iacc,
            0,
            0,
            $sequences->{$iacc},
            1,
            length($sequences->{$iacc}),
        ]);
    }
}

$select_viral_proteins_sth->finish;

$vinland->do('COPY sequences (protein_id, accession, is_canonical, is_mature, sequence, start, stop) FROM STDIN');

$vinland->pg_putcopydata(join("\t", @{$_}) . "\n") foreach (@sequences);

$vinland->pg_putcopyend();
