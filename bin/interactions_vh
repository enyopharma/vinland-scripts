#!/usr/bin/env perl
use strict;
use warnings;
use DBI;
use Sort::Key::Natural qw(natsort);
use Data::Dumper;

my $drakkar_db = shift @ARGV or die('no drakkar db');
my $vinland_db = shift @ARGV or die('no vinland db');

my $drakkar = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', $drakkar_db, $ENV{'DB_DEV_HOSTNAME'}),
    $ENV{'DB_DEV_USERNAME'},
    $ENV{'DB_DEV_PASSWORD'},
);

my $vinland = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', $vinland_db, $ENV{'DB_DEV_HOSTNAME'}),
    $ENV{'DB_DEV_USERNAME'},
    $ENV{'DB_DEV_PASSWORD'},
);

my $select_descriptions_sth = $drakkar->prepare('
    SELECT pmid, psimi_id, accession1, name1, accession2, name2
    FROM dataset
    WHERE type = \'vh\'
    AND state = \'curated\'
    AND is_obsolete1 IS FALSE
    AND is_obsolete2 IS FALSE
    AND deleted_at IS NULL
    ORDER BY accession1 ASC, accession2 ASC
');

my $select_method_sth = $vinland->prepare('
    SELECT * FROM methods WHERE psimi_id = ?
');

my $select_protein_sth = $vinland->prepare('
    SELECT * FROM proteins WHERE accession = ? AND name = ?
');

my %interactions = ();

$select_descriptions_sth->execute;

while (my $description = $select_descriptions_sth->fetchrow_hashref) {
    $select_method_sth->execute($description->{'psimi_id'});
    my $method = $select_method_sth->fetchrow_hashref;
    $select_method_sth->finish;

    die(sprintf('method not found %s', $description->{'psimi_id'})) unless $method;

    $select_protein_sth->execute($description->{'accession1'}, $description->{'name1'});
    my $protein1 = $select_protein_sth->fetchrow_hashref;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $description->{'accession1'}, $description->{'name1'})) unless $protein1;

    $select_protein_sth->execute($description->{'accession2'}, $description->{'name2'});
    my $protein2 = $select_protein_sth->fetchrow_hashref;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $description->{'accession2'}, $description->{'name2'})) unless $protein2;

    my $accession1 = $protein1->{'accession'};
    my $accession2 = $protein2->{'accession'};
    my $name2 = $protein2->{'name'};
    my $pmid = $description->{'pmid'};
    my $psimi_id = $description->{'psimi_id'};

    if ($interactions{$accession1}->{$accession2}->{$name2}) {
        $interactions{$accession1}->{$accession2}->{$name2}->{'publications'}->{$pmid} = 1;
        $interactions{$accession1}->{$accession2}->{$name2}->{'methods'}->{$psimi_id} = 1;
        $interactions{$accession1}->{$accession2}->{$name2}->{'is_binary'} = 1 if ($method->{'is_binary'});
        next;
    }

    $interactions{$accession1}->{$accession2}->{$name2} = {
        'protein1_id' => $protein1->{'id'},
        'protein2_id' => $protein2->{'id'},
        'publications' => {$pmid => 1},
        'methods' => {$psimi_id => 1},
        'is_binary' => $method->{'is_binary'},
    };
}

$select_descriptions_sth->finish;

$vinland->do('COPY interactions (type, protein1_id, protein2_id, sources, nb_publications, nb_methods, is_gold, is_binary) FROM STDIN');

foreach my $accession1 (natsort keys %interactions) {
    foreach my $accession2 (natsort keys %{$interactions{$accession1}}) {
        foreach my $name2 (natsort keys %{$interactions{$accession1}->{$accession2}}) {
            my $nb_publications = scalar keys %{$interactions{$accession1}->{$accession2}->{$name2}->{'publications'}};
            my $nb_methods = scalar keys %{$interactions{$accession1}->{$accession2}->{$name2}->{'methods'}};
            my $is_gold = ($nb_publications > 1 || $nb_methods > 1) ? 1 : 0;
            my $is_binary = $interactions{$accession1}->{$accession2}->{$name2}->{'is_binary'};

            $vinland->pg_putcopydata(join("\t", (
                'vh',
                $interactions{$accession1}->{$accession2}->{$name2}->{'protein1_id'},
                $interactions{$accession1}->{$accession2}->{$name2}->{'protein2_id'},
                '{ENYO}',
                $nb_publications,
                $nb_methods,
                $is_gold,
                $is_binary,
            )) . "\n");
        }
    }
}

$vinland->pg_putcopyend();
